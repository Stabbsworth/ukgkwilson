//---------------------------The User's Name--------------------------
//This .dic file has to do with setting the user's name and suffix/prefix. You may recall some of the functions mentioned here from bootend.dic. As you can see, you can spread functions across multiple .dic files if you like! Just remember my note in bootend.dic about the different between OnFunction and Function.

//This file is fairly short, so don't worry! It shouldn't take you too long to finish.

//Make sure to read the walkthrough (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//--OnTeachName
//This is called from the config menu in the main character's double click menu when the user says they want to change their name. If you look in menu.dic you'll see the code leading to here.

OnTeachName	
{
	if reference0 == "TEACHNAME" //ignore this
		{
		"\0\s[0]You want to change your name? Fine.\![open,inputbox,OnNameTeach,-1]\e"
		//This is the bit of dialogue you can rewrite if you want. Note that this line ends with \![open,inputbox,OnNameTeach,-1]. You may recognize this from bootend.dic. Essentially, this will lead to the next function. Make sure you don't delete that tag! The rest you can do whatever with though.
		}
	else
		{
		//This else statement is empty, and I'm mostly pointing this out to mention that you can do this too with your if/elses if you want! An empty else will simply cause your ghost not to do anything. I wouldn't get too crazy with this though.
		}
}


//--OnNameTeach
//This is where the user inputs their name and the ghost analyzes it. If you want your ghost to have specific responses to certain names, this is where you would add them. If not, you can erase all the way from the initial if down to the else statement, only leaving from ""\0\s[0]Do you want a title?" to "\![*]\q[-san,titlesan]\e". So it'd look something like

//OnNameTeach
// {
	//tempname = reference0
	
//"\0\s[0]Do you want a title?\w4\n\n[half]\![*]\q[Nope,titlenone]\n"
	//	--
	//	if presuffix == "masculine"
	//	{
	//	"\![*]\q[Mr.,titlemr]\n/
	//	\![*]\q[Master,titlemaster]\n"
	//	}
	//	elseif presuffix == "feminine" 
	//	{
	//	"\![*]\q[Ms.,titlems]\n/
	//	\![*]\q[Lady,titlelady]\n"
	//	}
	//	-- 
	//	"\![*]\q[-ssi,titlessi]\n/
	//	\![*]\q[-san,titlesan]\e"
// }

//If you did so. Otherwise, just fill things out as you like.

OnNameTeach
{
	tempname = reference0 //this stores the name value temporarily in reference0
	reference0 = TOLOWER(reference0) //changes it to lowercase
	reference0 = REPLACE(reference0," ","") //removes spaces
	reference0 = REPLACE(reference0,".","") //removes periods
	reference0 = REPLACE(reference0,"-","") //removes dashes
	//DEV: The above function is to simplify the whole bunch of if/else functions down here.
	if reference0 == "wilson" || reference0 == "higgsbury" //this checks to see if the user input the name "Girl" OR (that's what || means) "girl". You may want to put in a check here if the user tries to name themselves the same name as your characters, since that could be confusing in dialogue. Or just a response to any kind of strange name, like Barack Obama or Cindy Crawford or Mike Nelson or Random Hajile. Or you can prevent certain offensive names if you don't want users using them. Really the power is yours here. The OR operator there with "Girl" and "girl" is to cover people using lowercase as well. More on operators in the walkthrough page on coding.
	{
		if RAND(100) == 1
		{
			"\0\s[9]Quit kinning me, dammit.\![open,inputbox,OnNameTeach,-1]"
		}
		"\0\s[6]That's my name.\![open,inputbox,OnNameTeach,-1]"
		//Note here that it's using the same tag as above to reopen the text box to let the user put in another name. You can replace the dialogue with whatever as long as you keep the \![open,inputbox,OnNameTeach,-1] tag.
	}
	elseif reference0 == "willow" || reference0 == "wickerbottom" || reference0 == "webber" || reference0 == "wolfgang" || reference0 == "wurt" || reference0 == "winona" || reference0 == "warly" || reference0 == "wendy" || reference0 == "abigail" || reference0 == "woodie" || reference0 == "maxwell" || reference0 == "wes" || reference0 == "wigfrid" || reference0 == "wormwood" || reference0 == "wortox"
	{
		"\0\s[1]Hmn. \w8That's odd. \w8You've got the name of one of my friends\w8.\w8.\w8.\w8 Interesting...\e"
	}
	elseif reference0 == "wheeler" || reference0 == "wilba" || reference0 == "woodlegs" || reference0 == "wilbur" || reference0 == "walani"
	{
		"\0\s[1]That's an oddly familiar name, but I'll allow it.\e"
	}
	elseif reference0 == "charlie" || reference0 == "william"
	{
		"\0\s[1]...Hmn. \w8That name feels strangely familiar, somehow. \w8\s[9]I'm not sure why.\e"
	}
	elseif reference0 == "warbucks"
	{
		"\0\s[0]Hrm. \w8I don't... think I should allow this one, frankly.\![open,inputbox,OnNameTeach,-1]"
	}
	elseif reference0 == "simmy" || reference0 == "myst" || reference0 == "stubbs" || reference0 == "ruby" || reference0 == "cyber" || reference0 == "mei"
	{
		"\0\s[6]Oh. \w8It's YOU. \w8\w8\w8You people are disgusting, and I don't want any part of it.\-"
		//DEV: intended functionality. i don't want these freaks interacting with my work.
		//DEV: the last one in particular has drawn a faun version of wendy. nude. the second-last one accused me of sexualizing her when i pointed it out that maybe you shouldn't draw nude children.
		//DEV: you know, maybe it shouldn't fall on some minor to call out the shit you're doing, pato-ki.
	}
	elseif reference0 == "finn" || reference0 == "len" || reference0 == "berries" || reference0 == "atlas" || reference0 == "ree" || reference0 == "cecil" || reference0 == "lia" || reference0 == "mirth" || reference0 == "aesop"
	{
		"\0\s[6]Interesting. You're one of Stabbs' friends, aren't you?\e"
	}
	elseif reference0 == "stabbs" || reference0 == "stabbsworth" || reference0 == "percival"
	{
		"\0\s[6]What are you, the developer? Fine, I'll allow it.\e"
	}
	elseif reference0 == "clockblockers" || reference0 == "cb" || reference0 == "voidbarker" || reference0 == "barker"
	{
		"\0\s[0]Wow. That's certainly an old alias.\e"
	}
	elseif reference0 == "zarla"
	{
		"\0\s[0]You did the guide, didn't you?\e"
	}
	elseif reference0 == "celly" || reference0 == "excellynt" || reference0 == "lyntel"
	{
		"\0\s[0]Apparently you're the person that managed to get ghost-to-ghost communications set up. Hi.\e"
	}
	elseif reference0 == "fuck" || reference0 == "shit" || reference0 == "bitch" || reference0 == "bastard"
	{
		"\0\s[9]Oh, come on. Have some class, why don't you?\![open,inputbox,OnNameTeach,-1]"
	}
	elseif reference0 == "cunt" || reference0 == "retard" || reference0 == "retarded" || reference0 == "nigga" || reference0 == "nigger" || reference0 == "psycho" || reference0 == "psychopath" || reference0 == "sociopath"
	{
		"\0\s[6]Don't you put slurs in my fucking name input box, pal.\![open,inputbox,OnNameTeach,-1]"
		//DEV: I feel mildly guilty about this one, but it's mostly to keep white people from putting in a racial slur.
	}
	elseif reference0 == "daddy" || reference0 == "mummy" || reference0 == "mommy" //DEV: Sorry, ghouls and ghosts, this developer is against DDLG.
	{
		"\0\s[4]Er. No. Choose something else.\![open,inputbox,OnNameTeach,-1]"
	}
	elseif reference0 == "human"
	{
		"\0\s[6]I'm also human. That's just going to get confusing.\![open,inputbox,OnNameTeach,-1]"
	}
	elseif reference0 == "69" || reference0 == "420"
	{
		"\0\s[8]Nice.\e" //DEV: this is specifically a goof.
	}
	elseif reference0 == "" || reference0 == " " || reference0 == "   " //This is if the user doesn't put in a name at all, since that'd also get awkward in dialogue. Admittedly if a user REALLY wanted to put a ton of spaces as their name, the OR operators can only go so far, but this situation would mostly come up by accident I'd figure from a user hitting enter too quickly.
	{
		username = "NULL" //this sets the user's name to NULL.
		
		"\0\s[6]There's nothing... there. Hmn. \s[9]\w8Fine, I'll call you NULL for the moment.\e"
		//You can change this snip to whatever.
	}
	else //if the user put in something not specified above, ie a proper name the ghost can use. 
	{
		"\0\s[0]Do you want a title?\w4\n\n[half]\![*]\q[Nope,titlenone]\n"
		//You can replace "\0\s[0]Do you want a title?", but don't touch the rest of it.
		--
		if presuffix == "masculine" //this value was defined in bootend.dic and menu.dic when the user chose their pronouns. Thus, if someone chose he/him pronouns, they would be able to choose Mr. or Master.
		{
		"\![*]\q[Mr.,titlemr]\n/
		\![*]\q[Sir,titlesir]\n/
		\![*]\q[Master,titlemaster]\n"
		}
		elseif presuffix == "feminine" //Likewise, if someone chose she/her pronouns, they could choose Ms. or Lady.
		{
		"\![*]\q[Ms.,titlems]\n/
		\![*]\q[Miss,titlemiss]\n/
		\![*]\q[Lady,titlelady]\n"
		}
		-- //the titles after this are gender neutral and will appear for all users. If you want to remove the masculine and feminine titles entirely, just delete from the curly bracket one line up to the double dashes above "if presuffix == "masculine". That should leave only the neutral titles. Make sure to leave a set of double dashes above the neutral titles though.
		"\![*]\q[-ssi,titlessi]\n/
		\![*]\q[-san,titlesan]\n/
		\![*]\q[Ser,titleser]\e"
		//Note that the final line here ends with \e instead of \n/, since it is the final option. If you're copying and pasting to add new options, don't use the final line. More on that in the box below.
	}
}
	//*********************Advanced User Info*******************************
	//Adding new titles for your users to use is simply a matter of copy and pasting creatively. You can see how the ghost puts together the title and your name in the Select. sections below. Above, you simply need to add a new option and link it to a new Select. For example, you could add "\![*]\q[Mistress,titlemistress]\n/" between Ms. and Lady up there, then write up a Select.titlemistress below that fits the pattern of the others.
	
	//If you are removing the pronoun system entirely, what you'd want to do is erase everything in that else statement above regarding titles and replace it with something like this.
	//else
	//{
	//username = tempname
	//"\0\s[0]The user is now called %(username).\e"
	//}
	//***********************************************************************
	
	//The following Select.title[word] functions add the title to the user's name, then go to the final step, NameDone. You don't have to touch any of these. Advanced users, notice how these match the linked functions in the choices above.
	
Select.titlenone
{
		username = tempname
		NameDone
}

Select.titlemr
{
		username = "Mr. " + tempname
		NameDone
}

Select.titlemaster
{
		username = "Master " + tempname
		NameDone
}

Select.titlesir
{
		username = "Sir " + tempname
		NameDone
}
		
Select.titlems
{
		username = "Ms. " + tempname
		NameDone
}

Select.titlemiss
{
		username = "Miss " + tempname
		NameDone
}

Select.titlelady
{
		username = "Lady " + tempname
		NameDone
}

Select.titlessi
{
		username = tempname + "-ssi"
		NameDone
}

Select.titlesan
{
		username = tempname + "-san"
		NameDone
}

Select.titleser
{
		username = "Ser " + tempname
		NameDone
}

//--NameDone
//This is the final bit of dialogue your ghosts will have when the user changes their name.

NameDone
{
		"\0\s[0]The user is now called %(username).\e"
		"\0\s[1]Congratulations on your name change.\e"
		//Rewrite this as you like. Note the %(username) tag here, as you should be well familiar with by now from the walkthrough and the other .dic files. That will display the name the user just created.
}
